/*
    Applies across android app modules: {app, app-mini, app-tv}
 */
apply plugin: 'com.android.application'
apply from: "../commons.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}
//Increments versionCode by ABI type
def abiCodeMap = ['armeabi-v7a': '1', 'arm64-v8a': '2', 'x86': '4', 'x86_64': '5']
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = abiCodeMap.get(output.getFilter("ABI"))
        if(baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(variant.versionCode) + Integer.valueOf(baseVersionCode)
        }
    }
}

android {

    signingConfigs {
        release {
            if (!keystoreProperties.stringPropertyNames().isEmpty()) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"
        }
    }

    packagingOptions {
        exclude 'META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version'
    }
}

repositories {
    //noinspection JcenterRepositoryObsolete
    jcenter {
        content {
            includeGroup('com.jraska')
            includeGroup('tools.fastlane')
        }
    }
}

